---------------------------- MODULE raftActions ----------------------------

EXTENDS raftInit

----
\* Define state transitions

\* Modified to allow Restarts only for Leaders
\* Server i restarts from stable storage.
\* It loses everything but its currentTerm, votedFor, and log.
\* Also persists messages and instrumentation vars elections, maxc, leaderCount, entryCommitStats
Restart(i) ==
    /\ state[i] = Leader \* limit restart to leaders todo mc
    /\ state'          = [state EXCEPT ![i] = Follower]
    /\ votesResponded' = [votesResponded EXCEPT ![i] = {}]
    /\ votesGranted'   = [votesGranted EXCEPT ![i] = {}]
    /\ voterLog'       = [voterLog EXCEPT ![i] = [j \in {} |-> <<>>]]
    /\ nextIndex'      = [nextIndex EXCEPT ![i] = [j \in Server |-> 1]]
    /\ matchIndex'     = [matchIndex EXCEPT ![i] = [j \in Server |-> 0]]
    /\ commitIndex'    = [commitIndex EXCEPT ![i] = 0]
    /\ UNCHANGED <<messages, currentTerm, votedFor, log, instrumentationVars,hovercraftVars>>

\* Modified to restrict Timeout to just Followers
\* Server i times out and starts a new election. Follower -> Candidate
Timeout(i) == /\ state[i] \in {Follower} \*, Candidate
              /\ currentTerm[i] < MaxTerm
              /\ state' = [state EXCEPT ![i] = Candidate]
              /\ currentTerm' = [currentTerm EXCEPT ![i] = currentTerm[i] + 1]
              \* Most implementations would probably just set the local vote
              \* atomically, but messaging localhost for it is weaker.
              /\ votedFor' = [votedFor EXCEPT ![i] = Nil]
              /\ votesResponded' = [votesResponded EXCEPT ![i] = {}]
              /\ votesGranted'   = [votesGranted EXCEPT ![i] = {}]
              /\ voterLog'       = [voterLog EXCEPT ![i] = [j \in {} |-> <<>>]]
              /\ UNCHANGED <<messages, leaderVars, logVars, instrumentationVars,hovercraftVars>>

\* Modified to restrict Leader transitions, bounded by MaxBecomeLeader
\* Candidate i transitions to leader. Candidate -> Leader
BecomeLeader(i) ==
    /\ state[i] = Candidate
    /\ votesGranted[i] \in Quorum
    /\ leaderCount[i] < MaxBecomeLeader
    /\ state'      = [state EXCEPT ![i] = Leader]
    /\ nextIndex'  = [nextIndex EXCEPT ![i] =
                         [j \in Server |-> Len(log[i]) + 1]]
    /\ matchIndex' = [matchIndex EXCEPT ![i] =
                         [j \in Server |-> 0]]
    /\ leaderCount' = [leaderCount EXCEPT ![i] = leaderCount[i] + 1]
    /\ UNCHANGED <<messages, currentTerm, votedFor, candidateVars, logVars, maxc, entryCommitStats,hovercraftVars>>

\* Modified up to MaxTerm; Back To Follower
\* Any RPC with a newer term causes the recipient to advance its term first.
UpdateTerm(i, j, m) ==
    /\ m.mterm > currentTerm[i]
    /\ m.mterm < MaxTerm
    /\ currentTerm'    = [currentTerm EXCEPT ![i] = m.mterm]
    /\ state'          = [state       EXCEPT ![i] = Follower]
    /\ votedFor'       = [votedFor    EXCEPT ![i] = Nil]
       \* messages is unchanged so m can be processed further.
    /\ UNCHANGED <<messages, candidateVars, leaderVars, logVars, instrumentationVars,hovercraftVars>>

\***************************** REQUEST VOTE **********************************************
\* Message handlers
\* i = recipient, j = sender, m = message

\* Candidate i sends j a RequestVote request.
RequestVote(i, j) ==
    /\ state[i] = Candidate
    /\ j \notin votesResponded[i]
    /\ Send([mtype         |-> RequestVoteRequest,
             mterm         |-> currentTerm[i],
             mlastLogTerm  |-> LastTerm(log[i]),
             mlastLogIndex |-> Len(log[i]),
             msource       |-> i,
             mdest         |-> j])
    /\ UNCHANGED <<serverVars, candidateVars, leaderVars, logVars, instrumentationVars,hovercraftVars>>

\* Server i receives a RequestVote request from server j with
\* m.mterm <= currentTerm[i].
HandleRequestVoteRequest(i, j, m) ==
    LET logOk == \/ m.mlastLogTerm > LastTerm(log[i])
                 \/ /\ m.mlastLogTerm = LastTerm(log[i])
                    /\ m.mlastLogIndex >= Len(log[i])
        grant == /\ m.mterm = currentTerm[i]
                 /\ logOk
                 /\ votedFor[i] \in {Nil, j}
    IN /\ m.mterm <= currentTerm[i]
       /\ \/ grant  /\ votedFor' = [votedFor EXCEPT ![i] = j]
          \/ ~grant /\ UNCHANGED votedFor
       /\ Reply([mtype        |-> RequestVoteResponse,
                 mterm        |-> currentTerm[i],
                 mvoteGranted |-> grant,
                 \* mlog is used just for the `elections' history variable for
                 \* the proof. It would not exist in a real implementation.
                 mlog         |-> log[i],
                 msource      |-> i,
                 mdest        |-> j],
                 m)
       /\ UNCHANGED <<state, currentTerm, candidateVars, leaderVars, logVars, instrumentationVars,hovercraftVars>>

\* Server i receives a RequestVote response from server j with
\* m.mterm = currentTerm[i].
HandleRequestVoteResponse(i, j, m) ==
    \* This tallies votes even when the current state is not Candidate, but
    \* they won't be looked at, so it doesn't matter.
    /\ m.mterm = currentTerm[i]
    /\ votesResponded' = [votesResponded EXCEPT ![i] =
                              votesResponded[i] \cup {j}]
    /\ \/ /\ m.mvoteGranted
          /\ votesGranted' = [votesGranted EXCEPT ![i] =
                                  votesGranted[i] \cup {j}]
          /\ voterLog' = [voterLog EXCEPT ![i] =
                              voterLog[i] @@ (j :> m.mlog)]
       \/ /\ ~m.mvoteGranted
          /\ UNCHANGED <<votesGranted, voterLog>>
    /\ Discard(m)
    /\ UNCHANGED <<serverVars, votedFor, leaderVars, logVars, instrumentationVars,hovercraftVars>>

\* Responses with stale terms are ignored.
DropStaleResponse(i, j, m) ==
    /\ m.mterm < currentTerm[i]
    /\ Discard(m)
    /\ UNCHANGED <<serverVars, candidateVars, leaderVars, logVars, instrumentationVars,hovercraftVars>>

\***************************** AppendEntries **********************************************

AppendEntries(i, j) ==
    /\ state[i] = Leader
    /\ Len(log[i]) > 0
    /\ nextIndex[i][j] <= Len(log[i])
    /\ LET entryIndex == nextIndex[i][j]
           fullLogEntry == log[i][entryIndex]
           metadataToSend == << [ term |-> fullLogEntry.term,
                                  value |-> fullLogEntry.value ] >>
           entryKeyForStats == <<entryIndex, fullLogEntry.term>>
           prevLogIndex == entryIndex - 1
           prevLogTerm == IF prevLogIndex > 0 THEN log[i][prevLogIndex].term ELSE 0
       IN 
       /\ Send([mtype |-> AppendEntriesRequest,
               mterm |-> currentTerm[i],
               mprevLogIndex |-> prevLogIndex,
               mprevLogTerm |-> prevLogTerm,
               mentries |-> metadataToSend,
               mcommitIndex |-> Min({commitIndex[i], entryIndex}),
               msource |-> i,
               mdest |-> j])
       /\ entryCommitStats' = 
           IF entryKeyForStats \in DOMAIN entryCommitStats THEN
               [entryCommitStats EXCEPT ![entryKeyForStats] = 
                   [sentCount |-> entryCommitStats[entryKeyForStats].sentCount + 1,
                    ackCount |-> entryCommitStats[entryKeyForStats].ackCount,
                    committed |-> entryCommitStats[entryKeyForStats].committed]]
           ELSE
               entryCommitStats @@ 
                   [entryKeyForStats |-> [sentCount |-> 1, 
                                         ackCount |-> 0, 
                                         committed |-> FALSE]]
    /\ UNCHANGED <<currentTerm, state, votedFor, log, commitIndex, 
                  votesResponded, votesGranted, voterLog, leaderVars, 
                  leaderCount, maxc, hovercraftVars>>


\* Server i receives an AppendEntries request from server j with
\* m.mterm <= currentTerm[i]. This just handles m.entries of length 0 or 1, but
\* implementations could safely accept more by treating them the same as
\* multiple independent requests of 1 entry.


HandleAppendEntriesRequest(i, j, m) ==
    /\ i \in RaftServers(switchIndex) /\ j \in RaftServers(switchIndex)
    /\ LET logOk == \/ m.mprevLogIndex = 0
                    \/ /\ m.mprevLogIndex > 0
                       /\ m.mprevLogIndex <= Len(log[i])
                       /\ m.mprevLogTerm = log[i][m.mprevLogIndex].term
       IN
       /\ m.mterm <= currentTerm[i]
       /\ \/ /\ \* REJECT request
                \/ m.mterm < currentTerm[i]
                \/ /\ m.mterm = currentTerm[i] 
                   /\ state[i] = Follower 
                   /\ \lnot logOk
                \/ /\ m.mterm = currentTerm[i] 
                   /\ state[i] = Leader
             /\ Reply([mtype |-> AppendEntriesResponse,
                       mterm |-> currentTerm[i],
                       msuccess |-> FALSE,
                       mmatchIndex |-> 0,
                       msource |-> i,
                       mdest |-> j], m)
             /\ UNCHANGED <<currentTerm, state, votedFor, log, commitIndex,
                           votesResponded, votesGranted, voterLog,
                           nextIndex, matchIndex, leaderCount,
                           maxc, entryCommitStats,
                           switchIndex, switchBuffer, unorderedRequests, switchSentRecord>>
          \/ /\ \* RETURN to follower state
                m.mterm = currentTerm[i] 
                /\ state[i] = Candidate
             /\ state' = [state EXCEPT ![i] = Follower]
             /\ UNCHANGED <<messages, currentTerm, votedFor, log, commitIndex,
                           votesResponded, votesGranted, voterLog,
                           nextIndex, matchIndex, leaderCount,
                           maxc, entryCommitStats,
                           switchIndex, switchBuffer, unorderedRequests, switchSentRecord>>
          \/ /\ \* ACCEPT request (HOVERCRAFT LOGIC)
                m.mterm = currentTerm[i] 
                /\ state[i] = Follower 
                /\ logOk
             /\ LET index == m.mprevLogIndex + 1
                    metadataEntry == IF Len(m.mentries) > 0 THEN m.mentries[1] ELSE Nil
                    valueToMatch == IF metadataEntry /= Nil THEN metadataEntry.value ELSE Nil
                    value == IF valueToMatch \in DOMAIN switchBuffer
                                    THEN switchBuffer[valueToMatch].value
                                    ELSE Nil
                    entryToStore == IF metadataEntry /= Nil 
                                    /\ valueToMatch \in unorderedRequests[i]
                                    /\ value /= Nil 
                                   THEN
                                       [term |-> metadataEntry.term,
                                        value |-> value,
                                        payload |-> valueToMatch]
                                   ELSE Nil
                IN
                \/ /\ \* Case 1: MATCH/HEARTBEAT
                      \/ metadataEntry = Nil
                      \/ /\ entryToStore /= Nil
                         /\ Len(log[i]) >= index
                         /\ log[i][index].term = entryToStore.term
                         /\ log[i][index].payload = entryToStore.payload
                   /\ commitIndex' = [commitIndex EXCEPT ![i] = 
                       Max({commitIndex[i], Min({m.mcommitIndex, m.mprevLogIndex + Len(m.mentries)})})]
                   /\ Reply([mtype |-> AppendEntriesResponse,
                             mterm |-> currentTerm[i],
                             msuccess |-> TRUE,
                             mmatchIndex |-> m.mprevLogIndex + Len(m.mentries),
                             msource |-> i,
                             mdest |-> j], m)
                   /\ unorderedRequests' = [unorderedRequests EXCEPT ![i] = 
                       unorderedRequests[i] \ {valueToMatch}]
                   /\ UNCHANGED <<currentTerm, state, votedFor, log,
                                 votesResponded, votesGranted, voterLog,
                                 nextIndex, matchIndex, leaderCount,
                                 maxc, entryCommitStats,
                                 switchIndex, switchBuffer, switchSentRecord>>
                \/ /\ \* Case 2: CONFLICT
                      entryToStore /= Nil
                      /\ Len(log[i]) >= index
                      /\ \/ log[i][index].term /= entryToStore.term
                         \/ log[i][index].payload /= entryToStore.payload
                   /\ log' = [log EXCEPT ![i] = SubSeq(log[i], 1, index - 1)]
                   /\ Reply([mtype |-> AppendEntriesResponse,
                             mterm |-> currentTerm[i],
                             msuccess |-> FALSE,
                             mmatchIndex |-> 0,
                             msource |-> i,
                             mdest |-> j], m)
                   /\ UNCHANGED <<currentTerm, state, votedFor, commitIndex,
                                 votesResponded, votesGranted, voterLog,
                                 nextIndex, matchIndex, leaderCount,
                                 maxc, entryCommitStats,
                                 unorderedRequests, switchIndex, switchBuffer, switchSentRecord>>
                \/ /\ \* Case 3: APPEND
                      entryToStore /= Nil
                      /\ Len(log[i]) = m.mprevLogIndex
                      /\ valueToMatch \in unorderedRequests[i]
                      /\ value /= Nil
                   /\ log' = [log EXCEPT ![i] = Append(log[i], entryToStore)]
                   /\ commitIndex' = [commitIndex EXCEPT ![i] = 
                       Max({commitIndex[i], Min({m.mcommitIndex, m.mprevLogIndex + Len(m.mentries)})})]
                   /\ Reply([mtype |-> AppendEntriesResponse,
                             mterm |-> currentTerm[i],
                             msuccess |-> TRUE,
                             mmatchIndex |-> m.mprevLogIndex + Len(m.mentries),
                             msource |-> i,
                             mdest |-> j], m)
                   /\ unorderedRequests' = [unorderedRequests EXCEPT ![i] = 
                       unorderedRequests[i] \ {valueToMatch}]
                   /\ UNCHANGED <<currentTerm, state, votedFor,
                                 votesResponded, votesGranted, voterLog,
                                 nextIndex, matchIndex, leaderCount,
                                 maxc, entryCommitStats,
                                 switchIndex, switchBuffer, switchSentRecord>>


\* Server i receives an AppendEntries response from server j with
\* m.mterm = currentTerm[i].
HandleAppendEntriesResponse(i, j, m) ==
    /\ m.mterm = currentTerm[i]
    /\ \/ /\ m.msuccess \* successful
          /\ LET \*newMatchIndex == IF matchIndex[i][j] > m.mmatchIndex THEN matchIndex[i][j] ELSE m.mmatchIndex
                 newMatchIndex == m.mmatchIndex
                 entryKey == IF newMatchIndex > 0 /\ newMatchIndex <= Len(log[i])
                              THEN <<newMatchIndex, log[i][newMatchIndex].term>>
                              ELSE <<0, 0>> \* Invalid index or empty log
             IN \*/\ nextIndex'  = [nextIndex  EXCEPT ![i][j] = newMatchIndex + 1]
                /\ nextIndex'  = [nextIndex  EXCEPT ![i][j] = m.mmatchIndex + 1]
                /\ matchIndex' = [matchIndex EXCEPT ![i][j] = m.mmatchIndex]
                \*/\ matchIndex' = [matchIndex EXCEPT ![i][j] = newMatchIndex]
                /\ entryCommitStats' =
                     IF /\ entryKey /= <<0, 0>>
                        /\ entryKey \in DOMAIN entryCommitStats
                        /\ ~entryCommitStats[entryKey].committed
                     THEN [ entryCommitStats EXCEPT
                            ![entryKey] = [ sentCount |-> entryCommitStats[entryKey].sentCount,
                                            ackCount  |-> entryCommitStats[entryKey].ackCount + 1,
                                            committed |-> entryCommitStats[entryKey].committed ]
                        ]
                     ELSE entryCommitStats                     
       \/ /\ \lnot m.msuccess \* not successful
          /\ nextIndex' = [nextIndex EXCEPT ![i][j] =
                               Max({nextIndex[i][j] - 1, 1})]
          /\ UNCHANGED <<matchIndex, entryCommitStats>>
    /\ Discard(m)
    /\ UNCHANGED <<serverVars, candidateVars, logVars, maxc, leaderCount,hovercraftVars>>

\* Leader i advances its commitIndex.
\* This is done as a separate step from handling AppendEntries responses,
\* in part to minimize atomic regions, and in part so that leaders of
\* single-server clusters are able to mark entries committed.
AdvanceCommitIndex(i) ==
    /\ state[i] = Leader
    /\ LET \* The set of servers that agree up through index.
           Agree(index) == {i} \cup {k \in Server :
                                         matchIndex[i][k] >= index}
           \* The maximum indexes for which a quorum agrees
           agreeIndexes == {index \in 1..Len(log[i]) :
                                Agree(index) \in Quorum}
           \* New value for commitIndex'[i]
           newCommitIndex ==
              IF /\ agreeIndexes /= {}
                 /\ log[i][Max(agreeIndexes)].term = currentTerm[i]
              THEN
                  Max(agreeIndexes)
              ELSE
                  commitIndex[i]
           committedIndexes == { k \in Nat : /\ k > commitIndex[i]
                                             /\ k <= newCommitIndex }
           \* Identify the keys in entryCommitStats corresponding to newly committed entries
           keysToUpdate == { key \in DOMAIN entryCommitStats : key[1] \in committedIndexes }           
       IN /\ commitIndex' = [commitIndex EXCEPT ![i] = newCommitIndex]
          \* Update the 'committed' flag for the relevant entries in entryCommitStats
          /\ entryCommitStats' =
                [key \in DOMAIN entryCommitStats |->
                    IF key \in keysToUpdate
                    THEN [entryCommitStats[key] EXCEPT !.committed = TRUE]
                    ELSE entryCommitStats[key]
                ]

               
    /\ UNCHANGED <<messages, serverVars, candidateVars, leaderVars, log, maxc, leaderCount,hovercraftVars>>

\* Network state transitions

\* The network duplicates a message
DuplicateMessage(m) ==
    /\ Send(m)
    /\ UNCHANGED <<serverVars, candidateVars, leaderVars, logVars, instrumentationVars,hovercraftVars>>

\* The network drops a message
DropMessage(m) ==
    /\ Discard(m)
    /\ UNCHANGED <<serverVars, candidateVars, leaderVars, logVars, instrumentationVars,hovercraftVars>>
    
    

SwitchClientRequest(sw_id, leader_id, client_payload_value) ==
    \* Parameters:
    \* sw_idx: The ID of the node designated as the Switch.
    \* leader_id: The ID of the RaftServer that is the current Leader.
    \* client_payload_value: The actual payload/value from the client (of type Value).
      \* Preconditions:
    /\ sw_id = switchIndex  \* Check that the targeted sw_idx is the current Switch.
    /\ leader_id \in RaftServers(switchIndex)
    /\ state[leader_id] = Leader \* Must be initiated by a current leader.

    \* Ensure the request value is not already in the Switch's buffer.
    \* This implies client_payload_value must be unique for each distinct request.
    /\ client_payload_value \notin DOMAIN switchBuffer

    \* Action: Add the request to the Switch's buffer.
    \* switchBuffer maps: Value -> Record[value: Value]
    \* The key is the payload itself, and the record also stores the payload.
    /\ LET bufferEntry == [value |-> client_payload_value, payload |-> client_payload_value]
       IN switchBuffer' = switchBuffer @@ (client_payload_value :> bufferEntry)

    \* Unchanged variables:
    /\ UNCHANGED <<messages, currentTerm, state, votedFor, log, commitIndex,
                   votesResponded, votesGranted, voterLog, nextIndex, matchIndex,
                   leaderCount, maxc, entryCommitStats, switchIndex,
                   unorderedRequests, switchSentRecord>>
                   \* switchIndex is UNCHANGED as it's fixed.



SwitchClientRequestReplicate(sw_idx, target_server_id, v_key) ==
    \* Parameters:
    \* sw_idx: The ID of the node acting as the Switch.
    \* target_server_id: The RaftServer to which the request is being replicated.
    \* v_key: The key (which is a Value) of the request in switchBuffer.
   
    \* Enabling Conditions:
    /\ sw_idx = switchIndex  \* Ensure this action is for the designated Switch.
    /\ target_server_id \in RaftServers(switchIndex)
    /\ v_key \in DOMAIN switchBuffer         \* The request (identified by its value) must exist in the Switch's buffer.
    /\ v_key \notin switchSentRecord[target_server_id] \* Avoid replicating the same request value to the same server again
                                                     \* (ensures progress if this action is chosen repeatedly for same v_key).

    \* Action:
    \* 1. Add the request's value (v_key) to the target server's unorderedRequests set.
    \*    unorderedRequests maps: RaftServer -> Set[Value]
    /\ unorderedRequests' = [unorderedRequests EXCEPT ![target_server_id] =
                                unorderedRequests[target_server_id] \cup {v_key}]

    \* 2. Record that the Switch has sent this request value to this server.
    \*    switchSentRecord maps: RaftServer -> Set[Value]
    /\ switchSentRecord' = [switchSentRecord EXCEPT ![target_server_id] =
                                switchSentRecord[target_server_id] \cup {v_key}]

    \* Unchanged variables:
    /\ UNCHANGED <<messages, currentTerm, state, votedFor, log, commitIndex,
                   votesResponded, votesGranted, voterLog, nextIndex, matchIndex,
                   leaderCount, maxc, entryCommitStats,
                   switchBuffer, switchIndex>>
                   \* switchIndex is also UNCHANGED.
                   \* switchBuffer is only read, not modified by this action.
 
 
\* Modified. Leader i receives a switch request to add v to the log. up to MaxClientRequests.
LeaderAddLog(i, v) ==
    /\ state[i] = Leader
    /\ maxc < MaxClientRequests 
    /\ LET entryTerm == currentTerm[i]
           entry == [term |-> entryTerm, value |-> v,  payload |-> v]
           entryExists == \E j \in DOMAIN log[i] : log[i][j].value = v /\ log[i][j].term = entryTerm
           newLog == IF entryExists THEN log[i] ELSE Append(log[i], entry)
           newEntryIndex == Len(log[i]) + 1
           newEntryKey == <<newEntryIndex, entryTerm>>
       IN
        /\ log' = [log EXCEPT ![i] = newLog]
        /\ maxc' = IF entryExists THEN maxc ELSE maxc + 1
        /\ entryCommitStats' =
              IF ~entryExists /\ newEntryIndex > 0 \* Only add stats for truly new entries
              THEN entryCommitStats @@ (newEntryKey :> [ sentCount |-> 0, ackCount |-> 0, committed |-> FALSE ])
              ELSE entryCommitStats
    /\ UNCHANGED <<messages, serverVars, candidateVars, leaderVars, commitIndex, leaderCount,hovercraftVars>>


=============================================================================
\* Created by Ovidiu-Cristian Marcu

